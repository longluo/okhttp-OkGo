apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.lzy.demo"
        minSdkVersion rootProject.ext.appMinSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName_okgo
        flavorDimensions "default"
    }

    signingConfigs {
        appkey {
            keyAlias 'OkGo'
            keyPassword '111111'
            storeFile file('../appkey.jks')
            storePassword '111111'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.appkey
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            signingConfig signingConfigs.appkey
        }
    }
    productFlavors {
        miui {
            proguardFile './proguard-rules.pro'
            signingConfig signingConfigs.appkey
        }
        qihu360 {
            proguardFile './proguard-rules.pro'
            signingConfig signingConfigs.appkey
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/rxjava.properties'
    }
}

task makeApk {
    doLast {
        def versionName = rootProject.ext.versionName_okgo
        def appName = "okgo_v${versionName}.apk"
        copy {
            from "./build/outputs/apk/demo-miui-debug.apk"
            into "../"
            rename {
                appName
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "com.google.android.material:material:1.0.0-rc01"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"

    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:v2.0.7'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.readystatesoftware.chuck:library:1.1.0'

    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'

    implementation project(':okgo')
    implementation project(':okrx')
    implementation project(':okrx2')
    implementation project(':okserver')

    implementation 'com.lzy.widget:ninegridview:0.2.1'
    implementation 'com.lzy.widget:imagepicker:0.5.1'
    implementation 'com.lzy.widget:view-core:0.2.3'
}